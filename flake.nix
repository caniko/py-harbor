{
  description = "Reusable Python uv2nix and pyproject-nix infrastructure for Nix flakes";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";

    flake-utils.url = "github:numtide/flake-utils";

    pyproject-nix = {
      url = "github:pyproject-nix/pyproject.nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    uv2nix = {
      url = "github:pyproject-nix/uv2nix";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.pyproject-nix.follows = "pyproject-nix";
    };

    pyproject-build-systems = {
      url = "github:pyproject-nix/build-system-pkgs";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.pyproject-nix.follows = "pyproject-nix";
      inputs.uv2nix.follows = "uv2nix";
    };

    meta-harbor = {
      url = "git+https://codeberg.org/caniko/meta-harbor.git?ref=trunk";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "flake-utils";
    };
  };

  outputs = {
    self,
    nixpkgs,
    flake-utils,
    pyproject-nix,
    uv2nix,
    pyproject-build-systems,
    meta-harbor,
    ...
  }: let
    lib = import ./lib {
      inherit
        nixpkgs
        pyproject-nix
        uv2nix
        pyproject-build-systems
        meta-harbor
        ;
    };
  in
    {
      inherit lib;
    }
    // flake-utils.lib.eachDefaultSystem (
      system: let
        pkgs = lib.mkPkgs {inherit system;};
      in {
        formatter = pkgs.writeShellApplication {
          name = "py-harbor-fmt";
          runtimeInputs = [pkgs.nixfmt];
          text = ''
            if [ "$#" -eq 0 ]; then
              find . -name '*.nix' -print0 | xargs -0 nixfmt
            else
              exec nixfmt "$@"
            fi
          '';
        };

        devShells = {
          default = pkgs.mkShell {};
        };

        checks = import ./checks {
          inherit self pkgs system;
          harbor = lib;
        };
      }
    );
}
